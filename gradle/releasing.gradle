ext {
    DESCRIPTION = "A Gradle Plugin and library to determine which modules were affected in a commit."
    VERSION = "0.1.1"
    GIT_URL = 'https://github.com/Dropbox/AffectedModuleDetector'
    GROUP_ID = "com.dropbox.affectedmoduledetector"
    SONATYPE_SNAPSHOT_URL = "https://oss.sonatype.org/content/repositories/snapshots/"
    SONATYPE_STAGING_URL = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
}

group = GROUP_ID
version = VERSION
description = DESCRIPTION

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'Affected Module Detector'
                description = DESCRIPTION
                url = GIT_URL
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mplat-dbx'
                        name = 'Dropbox'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:dropbox/AffectedModuleDetector.git'
                    developerConnection = 'scm:git:ssh://github.com:dropbox/AffectedModuleDetector.git'
                    url = GIT_URL
                }
            }

            from components.java
        }
    }
    repositories {
        maven {
            url version.endsWith('SNAPSHOT') ? SONATYPE_SNAPSHOT_URL : SONATYPE_STAGING_URL
            credentials {
                username project.hasProperty("SONATYPE_NEXUS_USERNAME") ? project.property("SONATYPE_NEXUS_USERNAME") : "username"
                password project.hasProperty("SONATYPE_NEXUS_PASSWORD") ? project.property("SONATYPE_NEXUS_PASSWORD") : "password"
            }
        }
    }
}

signing {
    if (!version.endsWith('SNAPSHOT')) {
        sign publishing.publications.maven
    }
}
