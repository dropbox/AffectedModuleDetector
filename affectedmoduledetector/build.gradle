plugins {
    id 'java-library'
    id 'kotlin'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'signing'
}


apply from: rootProject.file("gradle/jacoco.gradle")

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation("junit:junit:4.13.1")
    testImplementation("com.nhaarman:mockito-kotlin:1.5.0")
    testImplementation("com.google.truth:truth:1.0.1")
}

ext {
    DESCRIPTION = "A Gradle Plugin and library to determine which modules were affected in a commit."
    VERSION = "0.1.1-SNAPSHOT"
    GIT_URL = 'https://github.com/Dropbox/AffectedModuleDetector'
    GROUP_ID = "com.dropbox.affectedmoduledetector"
    SONATYPE_SNAPSHOT_URL = "https://oss.sonatype.org/content/repositories/snapshots/"
    SONATYPE_STAGING_URL = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
}

group = GROUP_ID
version = VERSION
description = DESCRIPTION

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'Affected Module Detector'
                description = DESCRIPTION
                url = GIT_URL
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mplat-dbx'
                        name = 'Dropbox'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:dropbox/AffectedModuleDetector.git'
                    developerConnection = 'scm:git:ssh://github.com:dropbox/AffectedModuleDetector.git'
                    url = GIT_URL
                }
            }

            from components.java
        }
    }
    repositories {
        maven {
            url version.endsWith('SNAPSHOT') ? SONATYPE_SNAPSHOT_URL : SONATYPE_STAGING_URL
            credentials {
                username project.hasProperty("SONATYPE_USERNAME") ? project.property("SONATYPE_USERNAME") : "username"
                password project.hasProperty("SONATYPE_PASSWORD") ? project.property("SONATYPE_PASSWORD") : "password"
            }
        }
    }
}

gradlePlugin {
    plugins {
        affectedModuleDetectorPlugin {
            id = GROUP_ID
            implementationClass = "com.dropbox.affectedmoduledetector.AffectedModuleDetectorPlugin"
        }
    }
}

pluginBundle {
    website = GIT_URL
    vcsUrl = GIT_URL
    description = "A Gradle Plugin to determine which modules were affected in a commit."
    tags = ["module", "git", "detector", "dependency"]

    plugins {
        affectedModuleDetectorPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = "Affected Module Detector"
        }
    }
}

signing {
    sign publishing.publications.maven
}
