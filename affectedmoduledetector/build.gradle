plugins {
    id 'java-library'
    id 'kotlin'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.12.0"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation("junit:junit:4.13.1")
    testImplementation("com.nhaarman:mockito-kotlin:1.5.0")
    testImplementation("com.google.truth:truth:1.0.1")
}

ext {
    DESCRIPTION = "A Gradle Plugin and library to determine which modules were affected in a commit."
    VERSION = "0.1.0-SNAPSHOT"
    GIT_URL = 'https://github.com/Dropbox/AffectedModuleDetector'
    GROUP_ID = "com.dropbox.affectedmoduledetector"
}

group = GROUP_ID
version = VERSION
description = DESCRIPTION

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'Affected Module Detector'
                description = 'A concise description of my library'
                url = GIT_URL
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mplat-dbx'
                        name = 'Dropbox'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:dropbox/AffectedModuleDetector.git'
                    developerConnection = 'scm:git:ssh://github.com:dropbox/AffectedModuleDetector.git'
                    url = GIT_URL
                }
            }

            from components.java
        }
    }
}

gradlePlugin {
    plugins {
        affectedModuleDetectorPlugin {
            id = GROUP_ID
            implementationClass = "com.dropbox.affectedmoduledetector.AffectedModuleDetectorPlugin"
        }
    }
}

pluginBundle {
    website = GIT_URL
    vcsUrl = GIT_URL
    description = "A Gradle Plugin to determine which modules were affected in a commit."
    tags = ["module", "git", "detector", "dependency"]

    plugins {
        affectedModuleDetectorPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = "Affected Module Detector"
        }
    }
}
